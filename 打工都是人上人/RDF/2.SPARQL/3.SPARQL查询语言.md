## 1. 介绍

RDF是用于表示Web中信息的指示标记的图形数据格式。 RDF通常用于代表个人信息，社交网络，关于数字伪影的元数据，以及提供一种集成的手段，超出不同的信息来源。此规范定义了RDF的SPARQL查询语言的语法和语义。

RDF的SPARQL查询语言旨在满足RDF数据访问用例和要求[UCNR]和SPARQL新功能中的RDF数据访问工作组和RDF数据访问工作组所识别的用例和要求和理由[UCNR2]。

### 1.1 文件大纲

除非另有说明，否则本文档中的所有部分和附录都是规范性的。

本文的本节第1节介绍了SPARQL查询语言规范。它介绍了本规范文档的组织和整个规范中使用的约定。

该规范的第2节通过一系列示例查询和查询结果介绍了SPARQL查询语言本身。第3节继续引入SPARQL查询语言，其中包含更多示例，该示例演示了SPARQL在查询结果中出现的RDF术语上表达约束的能力。

第4节介绍了SPARQL查询语言语法的详细信息。它是语言的完整语法的同伴，并定义语法构造如何代表虹膜，空白节点，文字和变量。第4节还定义了几种语法构建体的含义，该构建体用作句法糖，以便更详细表达。

第5节介绍了基本图形模式和组图案，构建了更多复杂的SPARQL查询模式的构建块。将SPARQL图表模式组合成更大的图形图案的部分6,7和8个存在的构造构造。特别是，第6节介绍了对Query的一部分可选的能力;第7节介绍了表达替代图案模式的分离的能力;第8节介绍了对脱离信息的测试模式。

第9节将属性路径添加到图形模式匹配，给出了查询的紧凑表示以及匹配图中任意长度路径的能力。

第10节描述了SPARQL中可能的分配形式。

部分11介绍了组和聚集结果的机制，如第12节所述，可以将其作为子查询结合。

第13节介绍了对特定源图限制查询部分的能力。第13节还介绍了SPARQL的机制，用于定义查询的源图。

第14节是指单独的文档SPARQL 1.1联合查询。

第15节定义了通过排序，切片，投影，限制和从一系列解决方案排序，切片，投影，限制和删除重复来影响查询的解决方案的构造。

第16节定义了从不同形式产生结果的四种类型的SPARQL查询。

第17节定义SparQL的可扩展值测试和表达式框架。它介绍了可用于约束查询结果中出现的值的函数和运算符，并且还计算查询返回的新值。

第18节是SPARQL图表模式和溶液改性剂评估的正式定义。

第19节包含SPARQL查询和SPARQL更新语言的语法的规范定义，如EBNF表示法中的语法给出的。

## 1.2 文件惯例

### 1.2.1 名称空间

在本文档中，除非另有说明，否则示例假设以下命名空间前缀绑定：

| Prefix  | IRI                                           |
| ------- | --------------------------------------------- |
| `rdf:`  | `http://www.w3.org/1999/02/22-rdf-syntax-ns#` |
| `rdfs:` | `http://www.w3.org/2000/01/rdf-schema#`       |
| `xsd:`  | `http://www.w3.org/2001/XMLSchema#`           |
| `fn:`   | `http://www.w3.org/2005/xpath-functions#`     |
| `sfn:`  | `http://www.w3.org/ns/sparql#`                |

### 1.2.2 数据描述

本文档使用Turtle [Turtle]数据格式明确地显示每个三联人。乌龟允许虹膜与前缀缩写：

```turtle
@prefix dc:   <http://purl.org/dc/elements/1.1/> .
@prefix :     <http://example.org/book/> .
:book1  dc:title  "SPARQL Tutorial" 
```

### 1.2.3 结果描述

结果集以表格形式说明。

|    x    |          y           |  z   |
| :-----: | :------------------: | :--: |
| "Alice" | `<http://example/a>` |      |

“绑定”是一对（变量，RDF项）。在此结果集中，有三个变量：x，y和z（显示为列标题）。每个解决方案在表格的主体中显示为一行。这里，存在一个解决方案，其中变量x绑定到“Alice”，变量Y绑定到\<http://example/a>，并且变量z不绑定到RDF项。变量不必须在解决方案中绑定。

### 1.2.4 术语

SPARQL语言包括IRIs——一个省略空格的RDF URI的子集。请注意，SPARQL查询中的所有IRIs都是绝对的;它们可能或可能不包括片段标识符[RFC3987，第3.1节]。IRIs包括URI [RFC3986]和URL。 SPARQL语法中的缩写表单（相对虹膜和前缀名称）被解析为产生绝对虹膜。

以下术语在RDF概念和抽象语法[概念]中定义，并在SPARQL中使用：

- IRI(对应于概念和抽象语法术语“RDF URI引用”)

- 文字

- 词法形式

- 朴素文字

- 语言标签

- 被标记类型的文字

- 数据类型IRI(对应于概念和抽象语法术语“数据类型URI”)

- 空白节点

此外，我们定义以下术语：

- RDF项，包括IRIs，空白节点和文字

- 简单的文字，涵盖没有语言标记或数据类型IRI的文字

## 2. 简单的查询(知识性)

大多数SPARQL查询形式都包含一组称为**基本图形模式**的三元组模式。三元组模式类似RDF三元组，只是每个主语、谓语和对象都可以是一个变量。当RDF数据子图中的RDF术语可以被变量替换时，基本图模式*匹配*RDF数据的子图，匹配结果是一个等价于RDF数据子图的新RDF图。

### 2.1 写一个简单的查询

下面的示例展示了一个SPARQL查询，用于从给定的数据图中查找书籍的标题。查询由两部分组成：SELECT子句标识要显示在查询结果中的变量，其中WHERE子句提供基本图形模式以匹配数据图。该示例中的基本图形模式包括利用单个变量（?title）替换对象位置的单个三元组模式。

数据集：

```turtle
<http://example.org/book/book1> <http://purl.org/dc/elements/1.1/title> "SPARQL Tutorial" .
```

查询语句：

```SPARQL
SELECT ?title
WHERE
{
  <http://example.org/book/book1> <http://purl.org/dc/elements/1.1/title> ?title .
}
```

在上面的数据上，此查询有一个解决方案：

查询结果：



|       title       |
| :---------------: |
| "SPARQL Tutorial" |

### 2.2 多匹配

查询的结果是解决方案序列，对应于查询的图形模式与数据匹配的方式。对查询可能有零，一个或多个解决方案。

数据集：

```turtle
@prefix foaf:  <http://xmlns.com/foaf/0.1/> .

_:a  foaf:name   "Johnny Lee Outlaw" .
_:a  foaf:mbox   <mailto:jlow@example.com> .
_:b  foaf:name   "Peter Goodguy" .
_:b  foaf:mbox   <mailto:peter@example.org> .
_:c  foaf:mbox   <mailto:carol@example.org> .
```

查询语句：

```SPARQL
PREFIX foaf:   <http://xmlns.com/foaf/0.1/>
SELECT ?name ?mbox
WHERE
  { ?x foaf:name ?name .
    ?x foaf:mbox ?mbox }
```

查询结果：

|        name         |            mbox            |
| :-----------------: | :------------------------: |
| "Johnny Lee Outlaw" | <mailto:jlow@example.com>  |
|   "Peter Goodguy"   | <mailto:peter@example.org> |

每个解决方案都提供了一种方法，可以将所选变量绑定到RDF术语，以便查询模式与数据匹配。结果集给出了所有可能的解决方案。在上面的示例中，以下两个数据子集提供了两个匹配项。

```turtle
 _:a foaf:name  "Johnny Lee Outlaw" .
 _:a foaf:box   <mailto:jlow@example.com> .
```

```turtle
 _:b foaf:name  "Peter Goodguy" .
 _:b foaf:box   <mailto:peter@example.org> .
```

这是一个基本的图表模式匹配;查询模式中使用的所有变量必须在每个解决方案中绑定。

### 2.3 匹配RDF文字

以下数据包含三个RDF文字：

```SPARQL
@prefix dt:   <http://example.org/datatype#> .
@prefix ns:   <http://example.org/ns#> .
@prefix :     <http://example.org/ns#> .
@prefix xsd:  <http://www.w3.org/2001/XMLSchema#> .

:x   ns:p     "cat"@en .
:y   ns:p     "42"^^xsd:integer .
:z   ns:p     "abc"^^dt:specialDatatype .
```

请注意，在Turtle中，“cat”@en是一个具有词汇形式“cat”、语言标签“en”的RDF文字， “42”^^ xsd:xsd:integer是一个被标记类型的文字，其中数据类型`http://www.w3.org/2001/xmlschema#Integer`，“abc”^^ dt:specialDatatype是一个被标记类型的文字，其中数据类型`http://example.org/datatype#specialDatatype`。

此RDF数据是2.3.1-2.3.3节中查询示例的数据图。

#### 2.3.1 用语言标签匹配文字

SPARQL中的语言标签使用@和定义在Best Common Practice 47中的语言标签表示。

以下查询没有解决方案，因为“cat”与“cat”@en的RDF文字不同：

```turtle
SELECT ?v WHERE { ?v ?p "cat" }
```

  但是下面的查询将找到一个解决方案，变量v被绑定到:x，因为语言标记是指定的，并且匹配给定的数据:

```turtle
SELECT ?v WHERE { ?v ?p "cat"@en }
```

|             v             |
| :-----------------------: |
| <http://example.org/ns#x> |

#### 2.3.2 用数字类型匹配文字

SPARQL查询中的整数表示具有数据类型`xsd:integer`的RDF类型的文字。例如：42是“42”^^\<http://www.w3.org/2001/xmlschema#integer>的缩短形式。

以下查询中的模式具有一个解决方案，该解决方案与变量v绑定到:y。

```turtle
SELECT ?v WHERE { ?v ?p 42 }
```



|             v             |
| :-----------------------: |
| <http://example.org/ns#y> |

第4.1.2节定义了SPARQL简化形式的xsd:float和xsd:double。

#### 2.3.3 匹配文字与任意数据类型

以下查询具有变量v的解决方案，绑定为:z。查询处理器不必对数据类型的空间中的值有任何了解。因为词汇表单和数据类型IRI都匹配，所以文字匹配。

```turtle
SELECT ?v WHERE { ?v ?p "abc"^^<http://example.org/datatype#specialDatatype> }
```



|             v             |
| :-----------------------: |
| <http://example.org/ns#z> |

### 2.4 查询结果中的空白节点标签

查询结果可能包含空白节点。本文档中的示例结果集中的空节点以“_:”的形式编写，后跟一个空节点标签。

空白节点标签的作用域是一个结果集(参见“SPARQL查询结果XML格式”和“SPARQL 1.1查询结果JSON格式”)，或者，对于CONSTRUCT查询表单，是结果图。在结果集中使用相同的标签表示相同的空白节点。

数据集：

```turtle
@prefix foaf:  <http://xmlns.com/foaf/0.1/> .

_:a  foaf:name   "Alice" .
_:b  foaf:name   "Bob" .
```

查询语句：

```SPARQL
PREFIX foaf:   <http://xmlns.com/foaf/0.1/>
SELECT ?x ?name
WHERE  { ?x foaf:name ?name }
```



|  x   |  name   |
| :--: | :-----: |
| _:c  | "Alice" |
| _:d  |  "Bob"  |

上面的结果同样可以用不同的空白节点标签给出，因为结果中的标签仅表明解决方案中的RDF术语是否相同或不同。

|  x   |  name   |
| :--: | :-----: |
| _:r  | "Alice" |
| _:s  |  "Bob"  |

这两个结果具有相同的信息:用于匹配查询的空白节点在两个解决方案中是不同的。结果集中的标签_:a与具有相同标签的数据图中的空节点之间没有任何关系。

应用程序编写人员不应期望查询中的空白节点标签引用数据中的特定空白节点。

### 2.5 用表达式创建值

SPARQL 1.1允许从复杂表达式创建值。下面的查询显示了如何使用CONCAT函数连接foaf数据中的姓和名，然后使用SELECT子句中的表达式分配值，还使用绑定表单分配值。

数据集：

```turtle
@prefix foaf:  <http://xmlns.com/foaf/0.1/> .
          
_:a  foaf:givenName   "John" .
_:a  foaf:surname  "Doe" .
```

查询语句：

```turtle
PREFIX foaf:   <http://xmlns.com/foaf/0.1/>
SELECT ( CONCAT(?G, " ", ?S) AS ?name )
WHERE  { ?P foaf:givenName ?G ; foaf:surname ?S }
```

查询语句：

```SPARQL
PREFIX foaf:   <http://xmlns.com/foaf/0.1/>
SELECT ?name
WHERE  { 
   ?P foaf:givenName ?G ; 
      foaf:surname ?S 
   BIND(CONCAT(?G, " ", ?S) AS ?name)
}
```



|    name    |
| :--------: |
| "John Doe" |

### 2.6 构建RDF图形

SPARQL有几个查询表单。“SELECT”查询表单返回变量绑定。CONSTRUCT查询表单返回一个RDF图。该图形建立在模板的基础上，模板根据查询图模式的匹配结果生成RDF三元组。

数据集：

```turtle
@prefix org:    <http://example.com/ns#> .

_:a  org:employeeName   "Alice" .
_:a  org:employeeId     12345 .

_:b  org:employeeName   "Bob" .
_:b  org:employeeId     67890 .
```

查询语句：

```SPARQL
PREFIX foaf:   <http://xmlns.com/foaf/0.1/>
PREFIX org:    <http://example.com/ns#>

CONSTRUCT { ?x foaf:name ?name }
WHERE  { ?x org:employeeName ?name }
```

查询结果：

```turtle
@prefix foaf: <http://xmlns.com/foaf/0.1/> .
      
_:x foaf:name "Alice" .
_:y foaf:name "Bob" .
```

可以将上述查询结果利用RDF / XML序列化为：

```xml
<rdf:RDF
    xmlns:rdf="http://www.w3.org/1999/02/22-rdf-syntax-ns#"
    xmlns:foaf="http://xmlns.com/foaf/0.1/"
    >
  <rdf:Description>
    <foaf:name>Alice</foaf:name>
  </rdf:Description>
  <rdf:Description>
    <foaf:name>Bob</foaf:name>
  </rdf:Description>
</rdf:RDF>
```

## 3. RDF术语约束(知识性)

图模式匹配产生一个解决方案序列，其中每个解决方案都有一组RDF术语的变量绑定。SPARQL过滤器将解决方案限制为过滤器表达式计算结果为TRUE的解决方案。

本节将非正式地介绍SPARQL过滤器，它们的语义定义在“表达式和测试值”部分，其中有一个全面的函数库。本节中的示例展示一个输入图:

数据集：

```turtle
@prefix dc:   <http://purl.org/dc/elements/1.1/> .
@prefix :     <http://example.org/book/> .
@prefix ns:   <http://example.org/ns#> .

:book1  dc:title  "SPARQL Tutorial" .
:book1  ns:price  42 .
:book2  dc:title  "The Semantic Web" .
:book2  ns:price  23 .
```

### 3.1 限制字符串的值

像regex这样的SPARQL过滤函数可以测试RDF文字。regex只匹配字符串字面值。regex可以通过使用str函数来匹配其他字面值的词法形式。

查询语句：

```SPARQL
PREFIX  dc:  <http://purl.org/dc/elements/1.1/>
SELECT  ?title
WHERE   { ?x dc:title ?title
          FILTER regex(?title, "^SPARQL") 
        }
```

查询结果：

|       title       |
| :---------------: |
| "SPARQL Tutorial" |

正则表达式匹配可以使用“i”标志不区分大小写。

查询语句：

```SPARQL
PREFIX  dc:  <http://purl.org/dc/elements/1.1/>
SELECT  ?title
WHERE   { ?x dc:title ?title
          FILTER regex(?title, "web", "i" ) 
        }
```

查询结果：

|       title        |
| :----------------: |
| "The Semantic Web" |

正则表达式语言是由XQuery 1.0和XPath 2.0函数和操作符定义的，基于XML模式正则表达式。

### 3.2 限制数值

SPARQL过滤器可以限制算术表达式。

查询语句：

```SPARQL
PREFIX  dc:  <http://purl.org/dc/elements/1.1/>
PREFIX  ns:  <http://example.org/ns#>
SELECT  ?title ?price
WHERE   { ?x ns:price ?price .
          FILTER (?price < 30.5)
          ?x dc:title ?title . }
```

查询结果：

|       title        | price |
| :----------------: | :---: |
| "The Semantic Web" |  23   |

通过约束price变量，只有:book2匹配查询，因为只有:book2的价格小于30.5，这是筛选条件所要求的。

### 3.3 其他术语限制

除了数字类型之外，SPARQL还支持xsd:string、xsd:boolean和xsd:dateTime类型(参见操作数数据类型)。操作符映射章节描述了操作符，函数定义章节定义了可以应用于RDF术语的函数。

##  4. SPARQL语法

本节介绍SPARQL用于RDF术语和三元组模式的语法。完整的语法见第19节。

### 4.1 RDF术语语法

#### 4.1.1 IRIs格式

iri产品指定了IRIs的集合[RFC3987];IRIs是uri的概括[RFC3986]，它与uri和url完全兼容。PrefixedName产品指定一个带前缀的名称。下面描述了从带前缀的名称到IRI的映射。IRI引用(相对或绝对IRIs)由IRIREF产品指定，其中'<'和'>'分隔符不构成IRI引用的一部分。相对IRIs与第2.2节ABNF中IRI参考和[RFC3987]中IRI参考的直接参考值匹配，并按照下文所述解析为IRI。

RDF概念和抽象语法中定义的RDF术语集包括RDF URI引用，而SPARQL术语包括IRIs。RDF URI引用包含 "`<`", "`>`", '`"`' (双引号), 空格, "`{`", "`}`", "`|`", "`\`", "`^`", and "`" 它们都是不是IRIs。SPARQL查询对由此类RDF URI引用组成的RDF语句的行为没有定义。

##### 4.1.1.1 前缀名称

`PREFIX`关键字将前缀标签与IRI相关联。前缀名称分为前缀标签和本地部分，由冒号分隔“：”。通过连接与前缀和本地部分相关联的IRI，前缀名称映射到IRI。前缀标签或本地部分可以是空的。注意，SPARQL本地名允许前导数字，而XML本地名不允许。SPARQL本地名称也允许通过反斜杠字符转义(例如ns:id\=123)在IRIs中使用非字母数字字符。SPARQL本地名比CURIEs有更多的语法限制。

##### 4.1.1.2 相对IRIs

使用5.2节中的基本算法，相对IRIs与基本IRIs结合使用来作为统一资源标识符(Uniform Resource Identifier, URI):Generic Syntax[RFC3986]。既不执行基于语法的规范化，也不执行基于方案的规范化(在RFC3986的6.2.2和6.2.3节中描述)。IRI引用中额外允许的字符的处理方式与URI引用中不保留的字符的处理方式相同，在国际化资源标识符(IRIs) [RFC3987]中第6.5节定义。

BASE关键字定义了用于根据RFC3986第5.1.1节“嵌入内容的基本URI”解析相对IRIs的基本IRI。第5.1.2节“来自封装实体的基本URI”定义了基IRI如何来自封装文档，比如一个带有xml:base指令或具有内容位置标头的mime多部分文档。第5.1.3节中标识的“检索URI”是以“检索URI中的URI”为基础的，是用于检索特定SPARQL查询的URL。如果以上都没有指定基本URI，则使用默认的基本URI(第5.1.4节"Default Base URI")。

下面的片段是一些编写同一个IRI的不同方法：

```
<http://example.org/book/book1>
```

```
BASE <http://example.org/book/>
<book1>
```

```
PREFIX book: <http://example.org/book/>
book:book1
```

#### 4.1.2文字语法

文字的一般语法是一个字符串（用双引号"......"或单引号，'...'），其中包含可选的语言标记（由@标识）或（一个可选的数据类型IRI或一个前缀名称）（由^^标识）。

为了方便起见，整数可以直接写入(不带引号和显式数据类型IRI)，并被解释为数据类型为`xsd:integer`的类型文字;数字中有“.”但没有指数的十进制数被解释为`xsd:decimal`；带有指数的数字被解释为`xsd:double`。`xsd:boolean`类型的值也可以写成true或false。

为了便于编写本身包含引号或很长并包含换行符的文字值，SPARQL提供了一个附加的引号结构，其中文字用三个单引号或双引号括起来。

SPARQL中的文字语法的示例包括：

- `"chat"`
- `'chat'@fr` with language tag "fr"
- `"xyz"^^<http://example.org/ns/userDatatype>`
- `"abc"^^appNS:appDataType`
- `'''The librarian said, "Perhaps you would enjoy 'War and Peace'."'''`
- `1`, which is the same as `"1"^^xsd:integer`
- `1.3`, which is the same as `"1.3"^^xsd:decimal`
- `1.300`, which is the same as `"1.300"^^xsd:decimal`
- `1.0e6`, which is the same as `"1.0e6"^^xsd:double`
- `true`, which is the same as `"true"^^xsd:boolean`
- `false`, which is the same as `"false"^^xsd:boolean`

#### 4.1.3 查询变量的语法

查询变量使用"?"或"\$"来标记，"?"或"\$"不是变量名的一部分。在查询中，​\$abc和?abc标识相同的变量。变量的可能名称在SPARQL语法中给出。

#### 4.1.4 空白节点的语法

图模式中的空白节点充当变量，而不是对正在查询的数据中的特定空白节点的引用。

空白节点可以用标签形式表示，比如“`:abc`”，或者缩写形式“[]”。在查询语法中只在一个位置使用的空节点可以用[]表示。将使用一个唯一的空白节点来形成三元模式。对于标签为“`abc`”的空节点，空节点标签写为“`:abc`”。同一个查询中的两个不同的基本图形模式中不能使用相同的空白节点标签。

 `[:p :v]` 结构可以用在三种模式中。它创建一个用做所有<谓语，对象>对的主体的空白节点标签。创建的空白节点还可以在进一步三元组模式的主体和对象位置的使用。

以下两种形式：

```SPARQL
[ :p "v" ] .
```

```SPARQL
[] :p "v" .
```

分配一个唯一的空白节点标签(这里是“b57”)，相当于这样写：

```SPARQL
_:b57 :p "v" .
```

此分配的空白节点标签可以用作进一步三元组模式的主题或对象。例如，作为主题：

```SPARQL
[ :p "v" ] :q "w" .
```

相当于下面两个三元组：

```SPARQL
_:b57 :p "v" .
_:b57 :q "w" .
```

作为一个对象：

```SPARQL
:x :q [ :p "v" ] .
```

相当下面于两个三元组：

```SPARQL
:x  :q _:b57 .
_:b57 :p "v" .
```

缩略的空白节点语法可以与通用主题和通用谓词的其他缩写相结合。

```SPARQL
  [ foaf:name  ?name ;
    foaf:mbox  <mailto:alice@example.org> ]
```

这与为一些唯一分配的空白节点标签“`b18`”编写以下基本图形模式相同：

```SPARQL
  _:b18  foaf:name  ?name .
  _:b18  foaf:mbox  <mailto:alice@example.org> .
```

### 4.2 三元组模式的语法

三元组模式被写为主语、谓语和宾语，有一些简单的方法可以编写一些常见的三元模式结构。

下面的例子表达了相同的查询：

```SPARQL
PREFIX  dc: <http://purl.org/dc/elements/1.1/>
SELECT  ?title
WHERE   { <http://example.org/book/book1> dc:title ?title }  
PREFIX  dc: <http://purl.org/dc/elements/1.1/>
PREFIX  : <http://example.org/book/>
```
```SPARQL
SELECT  $title
WHERE   { :book1  dc:title  $title }
BASE    <http://example.org/book/>
PREFIX  dc: <http://purl.org/dc/elements/1.1/>
```
```SPARQL
SELECT  $title
WHERE   { <book1>  dc:title  ?title }
```

#### 4.2.1 谓词-对象列表

带有共同主语的三重模式可以写成这样，主语只需要写一次，并且可以通过使用“`;`”符号来表示多个三元组模式。

```SPARQL
?x  foaf:name  ?name ;
    foaf:mbox  ?mbox .
```

这与下面的三元组模式是相同的：

```SPARQL
?x  foaf:name  ?name .
?x  foaf:mbox  ?mbox .
```

#### 4.2.2 对象列表

如果三元组模式共用主语和谓语，则可以用“`,`”分隔对象。

```SPARQL
?x foaf:nick  "Alice" , "Alice_" .
```

与书写三元组模式相同:

```SPARQL
?x  foaf:nick  "Alice" .
?x  foaf:nick  "Alice_" .
```

对象列表可以与谓词-对象列表相结合:

```SPARQL
?x  foaf:name ?name ; foaf:nick  "Alice" , "Alice_" .
```

等价于：

```SPARQL
?x  foaf:name  ?name .
?x  foaf:nick  "Alice" .
?x  foaf:nick  "Alice_" .
```

#### 4.2.3 RDF集合

RDF集合可以使用“(element1 element2…)”语法以三元组模式编写。“`()`”是IRI http://www.w3.org/1999/02/22-rdf-syntax-ns#nil的另一种写法。当与集合元素(例如(1 ?x 3 4))一起使用时，将为集合分配带有空节点的三元组模式。集合头部的空白节点可以用作其他三元模式中的主体或对象。集合语法分配的空白节点不会出现在查询的其他地方。

```
(1 ?x 3 4) :p "w" .
```

是语法糖(注意b0, b1, b2和b3不出现在查询的其他任何地方):

```SPARQL
_:b0  rdf:first  1 ;
      rdf:rest   _:b1 .
_:b1  rdf:first  ?x ;
      rdf:rest   _:b2 .
_:b2  rdf:first  3 ;
      rdf:rest   _:b3 .
_:b3  rdf:first  4 ;
      rdf:rest   rdf:nil .
_:b0  :p         "w" . 
```

RDF集合可以被嵌套，并且可以包含其他语法形式:

```SPARQL
(1 [:p :q] ( 2 ) ) .
```

是语法糖:

```SPARQL
_:b0  rdf:first  1 ;
      rdf:rest   _:b1 .
_:b1  rdf:first  _:b2 .
_:b2  :p         :q .
_:b1  rdf:rest   _:b3 .
_:b3  rdf:first  _:b4 .
_:b4  rdf:first  2 ;
      rdf:rest   rdf:nil .
_:b3  rdf:rest   rdf:nil .
```

#### 4.2.4 rdf: type

关键字“a”可以用作三元模式中的谓词，是IRI http://www.w3.org/1999/02/22-rdf-syntax-ns#type的备选项，并且区分大小写。

```SPARQL
?x  a  :Class1 .
[ a :appClass ] :p "v" .
```

是语法糖:

```SPARQL
 ?x    rdf:type  :Class1 .
_:b0  rdf:type  :appClass .
_:b0  :p        "v" .
```

## 5. 图模式

SPARQL是基于图模式匹配的。更复杂的图模式可以通过多种方式组合较小的模式:

- 基本图模式，其中一组三元模式必须匹配

- 组图模式，其中一组图模式必须全部匹配

- 可选的图形模式，其中附加的模式可以扩展解决方案

- 备选图模式，尝试两个或多个可能的模式

- 命名图上的模式，其中模式与命名图匹配

在本节中，我们将描述通过连接来组合模式的两种形式:基本图模式(组合了三元组模式)和组图模式(组合了所有其他图模式)。

查询中最外部的图形模式称为查询模式。它在语法上由GroupGraphPattern在

```SPARQL
[17]  WhereClause  ::=  WHERE?  GroupGraphPattern
```
上定义。

### 5.1 基本图形模式

基本图形模式是三重模式的集合。SPARQL图模式匹配是根据组合匹配基本图模式的结果来定义的。

带有可选过滤器的三元组模式序列组成一个基本图模式。任何其他图模式都包含了一个基本图形模式。

#### 5.1.1 空节点标签

当使用`_:abc`形式的空白节点时，空白节点的标签的作用域是基本图模式。在任何查询中，标签只能在单个基本图模式中使用。

#### 5.1.2 扩展基本图模式匹配

SPARQL使用简单蕴涵定义的子图匹配来评估基本图模式。SPARQL可以扩展到以下所述的特定条件下的其他形式的蕴涵。SPARQL 1.1隐含机制文档描述了几种特定的隐含机制。

### 5.2 组图模式

在SPARQL查询字符串中，组图形模式用大括号:{}分隔。例如，该查询的查询模式是一个基本图形模式的组图形模式。

```SPARQL
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>
SELECT ?name ?mbox
WHERE  {
          ?x foaf:name ?name .
          ?x foaf:mbox ?mbox .
       }
```

从将三元组模式组合成两个基本图模式的查询中可以获得相同的解决方案。例如，下面的查询有一个不同的结构，但会产生与前面的查询相同的解决方案:

```SPARQL
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>
SELECT ?name ?mbox
WHERE  { { ?x foaf:name ?name . }
         { ?x foaf:mbox ?mbox . }
       }
```

#### 5.2.1 空组图模式

该组图模式：

```SPARQL
{ }
```

用一个不绑定任何变量的解决方案匹配任何图(包括空图)。例如:

```SPARQL
SELECT ?x
WHERE {}
```

与一个解决方案匹配，其中变量x不受限制。

#### 5.2.2 过滤器范围

由关键字FILTER表示的约束是对出现筛选器的整个组的解决方案的约束。以下模式都有相同的解决方案:

```SPARQL
{  ?x foaf:name ?name .
   ?x foaf:mbox ?mbox .
   FILTER regex(?name, "Smith")
}
```
```SPARQL
{  FILTER regex(?name, "Smith")
   ?x foaf:name ?name .
   ?x foaf:mbox ?mbox .
}
```
```SPARQL
{  ?x foaf:name ?name .
   FILTER regex(?name, "Smith")
   ?x foaf:mbox ?mbox .
}
```

#### 5.2.3 组图模式示例

```SPARQL
{
   ?x foaf:name ?name .
   ?x foaf:mbox ?mbox .
 }
```

是由一个基本图形模式组成的一组，该基本图形模式由两个三元组模式组成。

```SPARQL
{
   ?x foaf:name ?name . FILTER regex(?name, "Smith")
   ?x foaf:mbox ?mbox .
}
```

是由一个基本图模式和一个过滤器组成的一组，该基本图模式由两个三元图模式组成，过滤器不会将基本图模式分解为两个基本图模式。

```SPARQL
{
   ?x foaf:name ?name .
   {}
   ?x foaf:mbox ?mbox .
 }
```

是一个由三个元素组成的组，一个由一个三元组组成的基本图模式，一个空组，以及另一个由一个三元组组成的基本图模式。

## 6. 引入可选值

基本图模式允许应用程序在整个查询模式必须匹配的情况下进行查询，这样才能有一个解决方案。对于仅包含具有至少一个基本图模式的组图模式的查询的每个解决方案，每个变量都绑定到解决方案中的RDF术语。然而，规则的、完整的结构不能在所有RDF图中都假设。如果查询允许将信息添加到有信息可用的解决方案中，但不会因为查询模式的某些部分不匹配而拒绝该解决方案，这将非常有用。可选匹配提供了这种功能:如果可选部分不匹配，它不会创建绑定，但不会消除解决方案。

图形模式的可选部分可以用`OPTIONAL`关键字应用于图形模式的语法指定:

```SPARQL
pattern OPTIONAL { pattern }
```

句法形式：

```SPARQL
{ OPTIONAL { pattern } }
```

等价于：

```SPARQL
{ { } OPTIONAL { pattern } }
```

可选关键字是左关联的：

```SPARQL
pattern OPTIONAL { pattern } OPTIONAL { pattern }
```

与

```SPARQL
{ pattern OPTIONAL { pattern } } OPTIONAL { pattern }
```

一样。

数据集：

```turtle
@prefix foaf:       <http://xmlns.com/foaf/0.1/> .
@prefix rdf:        <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .

_:a  rdf:type        foaf:Person .
_:a  foaf:name       "Alice" .
_:a  foaf:mbox       <mailto:alice@example.com> .
_:a  foaf:mbox       <mailto:alice@work.example> .

_:b  rdf:type        foaf:Person .
_:b  foaf:name       "Bob" .
```

查询语句：

```SPARQL
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
SELECT ?name ?mbox
WHERE  { ?x foaf:name  ?name .
         OPTIONAL { ?x  foaf:mbox  ?mbox }
       }
```

根据以上数据，查询结果为：

|  name   |            mbox             |
| :-----: | :-------------------------: |
| "Alice" | <mailto:alice@example.com>  |
| "Alice" | <mailto:alice@work.example> |
|  "Bob"  |                             |

在名称为“Bob”的解决方案中，没有mbox的值。

该查询查找数据中的人名。如果有一个带有谓词`mbox`和相同主体的三元组，那么解决方案也将包含该三元组的对象。在本例中，查询的可选匹配部分只给出了一个单一的三元组模式，但通常，可选部分可以是任何图模式。整个可选图模式必须与可选图模式匹配，才能影响查询解决方案。

### 6.2 可选模式匹配约束

约束可以用可选的图形模式给出。例如:

```turtle
@prefix dc:   <http://purl.org/dc/elements/1.1/> .
@prefix :     <http://example.org/book/> .
@prefix ns:   <http://example.org/ns#> .

:book1  dc:title  "SPARQL Tutorial" .
:book1  ns:price  42 .
:book2  dc:title  "The Semantic Web" .
:book2  ns:price  23 .

```
```SPARQL
PREFIX  dc:  <http://purl.org/dc/elements/1.1/>
PREFIX  ns:  <http://example.org/ns#>
SELECT  ?title ?price
WHERE   { ?x dc:title ?title .
          OPTIONAL { ?x ns:price ?price . FILTER (?price < 30) }
        }
```



|       title        | price |
| :----------------: | :---: |
| "SPARQL Tutorial"  |       |
| "The Semantic Web" |  23   |

标题为“SPARQL教程”的书没有显示价格，因为可选的图形模式并没有导致涉及变量“`price`”的解决方案。

### 6.3 多种可选图形模式

图模式是递归定义的。一个图模式可以有零个或多个可选的图模式，查询模式的任何部分都可以有一个可选的部分。在本例中，有两个可选的图形模式。

数据集：

```turtle
@prefix foaf:       <http://xmlns.com/foaf/0.1/> .

_:a  foaf:name       "Alice" .
_:a  foaf:homepage   <http://work.example.org/alice/> .

_:b  foaf:name       "Bob" .
_:b  foaf:mbox       <mailto:bob@work.example> .
```

查询语句：

```SPARQL
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
SELECT ?name ?mbox ?hpage
WHERE  { ?x foaf:name  ?name .
         OPTIONAL { ?x foaf:mbox ?mbox } .
         OPTIONAL { ?x foaf:homepage ?hpage }
       }
```

查询结果：

|  name   |           mbox            |              hpage               |
| :-----: | :-----------------------: | :------------------------------: |
| "Alice" |                           | <http://work.example.org/alice/> |
|  "Bob"  | <mailto:bob@work.example> |                                  |

## 7. 匹配方案

SPARQL提供了一种组合图形模式的方法，以便几个可选图形模式中的一个可以匹配。如果有多个备选方案匹配，则找到所有可能的模式解决方案。

模式可选方案在语法上由`UNION`关键字指定。

数据集：

```turtle
@prefix dc10:  <http://purl.org/dc/elements/1.0/> .
@prefix dc11:  <http://purl.org/dc/elements/1.1/> .

_:a  dc10:title     "SPARQL Query Language Tutorial" .
_:a  dc10:creator   "Alice" .

_:b  dc11:title     "SPARQL Protocol Tutorial" .
_:b  dc11:creator   "Bob" .

_:c  dc10:title     "SPARQL" .
_:c  dc11:title     "SPARQL (updated)" .
```

查询语句：

```SPARQL
PREFIX dc10:  <http://purl.org/dc/elements/1.0/>
PREFIX dc11:  <http://purl.org/dc/elements/1.1/>

SELECT ?title
WHERE  { { ?book dc10:title  ?title } UNION { ?book dc11:title  ?title } }
```

查询结果：

|              title               |
| :------------------------------: |
|    "SPARQL Protocol Tutorial"    |
|             "SPARQL"             |
|        "SPARQL (updated)"        |
| "SPARQL Query Language Tutorial" |

该查询查找数据中书籍的标题，无论该标题是使用版本1.0还是版本1.1中的Dublin Core properties记录的。为了准确地确定信息是如何记录的，查询可以为这两个选项使用不同的变量:

```SPARQL
PREFIX dc10:  <http://purl.org/dc/elements/1.0/>
PREFIX dc11:  <http://purl.org/dc/elements/1.1/>

SELECT ?x ?y
WHERE  { { ?book dc10:title ?x } UNION { ?book dc11:title  ?y } }
```



|                x                 |             y              |
| :------------------------------: | :------------------------: |
|                                  |     "SPARQL (updated)"     |
|                                  | "SPARQL Protocol Tutorial" |
|             "SPARQL"             |                            |
| "SPARQL Query Language Tutorial" |                            |

对于联合的左分支的解，这将返回变量x界的结果，而对于右分支的解，则返回变量y界的结果。如果联合模式的任何一部分都不匹配，那么图形模式就不匹配。

UNION模式结合了图形模式;每一种可能性都可以包含一个以上的三元组模式:

```SPARQL
PREFIX dc10:  <http://purl.org/dc/elements/1.0/>
PREFIX dc11:  <http://purl.org/dc/elements/1.1/>

SELECT ?title ?author
WHERE  { { ?book dc10:title ?title .  ?book dc10:creator ?author }
         UNION
         { ?book dc11:title ?title .  ?book dc11:creator ?author }
       }
```



|              title               | author  |
| :------------------------------: | :-----: |
| "SPARQL Query Language Tutorial" | "Alice" |
|    "SPARQL Protocol Tutorial"    |  "Bob"  |

该查询将只匹配一本书，如果它具有来自相同版本Dublin Core的标题谓词和创建者谓词。

## 8. 否定

SPARQL查询语言包含了两种类型的否定，一种基于根据一个图模式在被过滤的查询解决方案的上下文中是否匹配来过滤结果，另一种基于删除与另一个模式相关的解决方案。

### 8.1 使用图形模式过滤

查询解决方案的筛选是在使用NOT EXISTS和EXISTS的筛选表达式中完成的。注意，筛选范围规则适用于出现筛选器的整个组。

#### 8.1.1 测试模式的缺失

给定出现筛选器的组图模式中的变量值，NOT EXISTS筛选器表达式测试图模式是否与数据集不匹配。它不会生成任何额外的绑定。

Data:

```turtle
@prefix  :       <http://example/> .
@prefix  rdf:    <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix  foaf:   <http://xmlns.com/foaf/0.1/> .

:alice  rdf:type   foaf:Person .
:alice  foaf:name  "Alice" .
:bob    rdf:type   foaf:Person .     
```

Query:

```SPARQL
PREFIX  rdf:    <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX  foaf:   <http://xmlns.com/foaf/0.1/> 

SELECT ?person
WHERE 
{
    ?person rdf:type  foaf:Person .
    FILTER NOT EXISTS { ?person foaf:name ?name }
}     
```

Query Result:

|        person        |
| :------------------: |
| <http://example/bob> |

#### 8.1.2 测试模式是否存在

还提供了EXISTS筛选器表达式。它测试是否可以在数据中找到模式;它不会生成任何额外的绑定。

Query:

```
PREFIX  rdf:    <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX  foaf:   <http://xmlns.com/foaf/0.1/> 

SELECT ?person
WHERE 
{
    ?person rdf:type  foaf:Person .
    FILTER EXISTS { ?person foaf:name ?name }
}
```

Query Result:

|         person         |
| :--------------------: |
| <http://example/alice> |

### 8.2 移除解决方案

SPARQL中提供的另一种类型的否定是MINUS，它对两个参数都求值，然后计算左边的解，这些解与右边的解不兼容。

Data:

```
@prefix :       <http://example/> .
@prefix foaf:   <http://xmlns.com/foaf/0.1/> .

:alice  foaf:givenName "Alice" ;
        foaf:familyName "Smith" .

:bob    foaf:givenName "Bob" ;
        foaf:familyName "Jones" .

:carol  foaf:givenName "Carol" ;
        foaf:familyName "Smith" .
```

Query:

```
PREFIX :       <http://example/>
PREFIX foaf:   <http://xmlns.com/foaf/0.1/>

SELECT DISTINCT ?s
WHERE {
   ?s ?p ?o .
   MINUS {
      ?s foaf:givenName "Bob" .
   }
}
```

Results:

|           s            |
| :--------------------: |
| <http://example/carol> |
| <http://example/alice> |

### 8.3 不存在与负的关系与区别

`NOT EXISTS`和`MINUS`表示考虑否定的两种方式，一种基于测试数据中是否存在模式(给定查询模式已经确定的绑定)，另一种基于对两个模式的评估来删除匹配项。在某些情况下，他们可以给出不同的答案。

#### 8.3.1 举例:共享变量

```
@prefix : <http://example/> .
:a :b :c .
```
```
SELECT *
{ 
  ?s ?p ?o
  FILTER NOT EXISTS { ?x ?y ?z }
}
```

因为{x ?y ?z}匹配给定的任何?s ?p ?o，所以不存在{?x ?y ?z}消除了任何解，所以结果集没有解。


```
s p o
```

而用减号，第一部分之间没有共享变量(?s ?p ?o)和(?x ?y ?z)，因此没有删除绑定。

```
SELECT *
{ 
   ?s ?p ?o 
   MINUS 
     { ?x ?y ?z }
}
```

Results:

|         s          |         p          |         o          |
| :----------------: | :----------------: | :----------------: |
| <http://example/a> | <http://example/b> | <http://example/c> |

#### 8.3.2 示例:固定模式

另一种情况是在示例中有一个具体的模式(没有变量):

```
PREFIX : <http://example/>
SELECT * 
{ 
  ?s ?p ?o 
  FILTER NOT EXISTS { :a :b :c }
}
```

计算结果为没有查询解决方案的结果集:

Results:

```
s p o
```

然而

```
PREFIX : <http://example/>
SELECT * 
{ 
  ?s ?p ?o 
  MINUS { :a :b :c }
}
```

使用一个查询解决方案计算结果集:

Results:

|         s          |         p          |         o          |
| :----------------: | :----------------: | :----------------: |
| <http://example/a> | <http://example/b> | <http://example/c> |

因为没有绑定匹配，所以没有消除任何解决方案。

#### 8.3.3 举例:内部过滤器

在过滤器中，来自组的变量在作用域中，这也会产生差异。在本例中，NOT EXISTS中的过滤器可以访问所考虑的解决方案的值?n。

```
@prefix : <http://example.com/> .
:a :p 1 .
:a :q 1 .
:a :q 2 .

:b :p 3.0 .
:b :q 4.0 .
:b :q 5.0 .
```

当使用不存在的过滤器时，测试是对x:p ?n的每个可能的解决方案:

```
PREFIX : <http://example.com/>
SELECT * WHERE {
        ?x :p ?n
        FILTER NOT EXISTS {
                ?x :q ?m .
                FILTER(?n = ?m)
        }
}
```

|           x            |  n   |
| :--------------------: | :--: |
| <http://example.com/b> | 3.0  |

而使用MINUS时，模式内部的过滤器没有?n的值，它总是未绑定的:

```
PREFIX : <http://example/>
SELECT * WHERE {
        ?x :p ?n
        MINUS {
                ?x :q ?m .
                FILTER(?n = ?m)
        }
}
```

|           x            |  n   |
| :--------------------: | :--: |
| <http://example.com/b> | 3.0  |
| <http://example.com/a> |  1   |

## 9. 属性路径

属性路径是两个图节点之间通过图的可能路径。一个简单的例子是长度为1的属性路径，这是一个三元组模式。路径的末尾可以是RDF术语或变量。变量不能作为路径本身的一部分，只能作为路径的末端。

属性路径允许为一些SPARQL基本图模式提供更简洁的表达式，它们还添加了通过任意长度路径匹配两个资源连接性的能力。

### 9.1 属性路径语法

在下面的描述中，iri要么是一个完整的iri，要么是一个带有前缀的名称的缩写，或者是关键字a. elt是一个路径元素，它本身可以由路径构造组成。

以下定义了一些路径的简单写法：

| 语法形式                               | 属性路径表达式名称 | 匹配                                                         |
| -------------------------------------- | ------------------ | ------------------------------------------------------------ |
| `iri`                                  | PredicatePath      | An IRI. A path of length one.                                |
| `^elt`                                 | InversePath        | Inverse path (object to subject).                            |
| `elt1 / elt2`                          | SequencePath       | A sequence path of `*elt1*` followed by `*elt2*`.            |
| `elt1 | elt2`                          | AlternativePath    | A alternative path of `*elt1*` or `*elt2*` (all possibilities are tried). |
| `elt*`                                 | ZeroOrMorePath     | A path that connects the subject and object of the path by zero or more matches of `*elt*`. |
| `elt+`                                 | OneOrMorePath      | A path that connects the subject and object of the path by one or more matches of `*elt*`. |
| `elt?`                                 | ZeroOrOnePath      | A path that connects the subject and object of the path by zero or one matches of `*elt*`. |
| `!iri` or `!(iri1| ...|irin)`          | NegatedPropertySet | Negated property set. An IRI which is not one of `*irii*`. `!*iri*` is short for `!*(iri)*`. |
| `!^iri` or `!(^iri1| ...|^irin)`       | NegatedPropertySet | Negated property set where the excluded matches are based on reversed path. That is, not one of *iri1*...*irin* as reverse paths. `!^*iri*` is short for `!(^*iri*)`. |
| `!(iri1| ...|irij|^irij+1| ...|^irin)` | NegatedPropertySet | A combination of forward and reverse properties in a negated property set. |
| `(elt)`                                |                    | A group path `*elt*`, brackets control precedence.           |

在属性集合collections中，IRI和Inverse IRI的顺序是不重要的，可以以混合顺序出现。

语法形式的优先级从高到低:

1. IRI, prefixed names
2. Negated property sets
3. Groups
4. Unary operators *, ? and +
5. Unary ^ inverse links
6. Binary operator /
7. Binary operator |

组内优先级从左到右。

### 9.2 例子

备选方案:匹配一种或两种可能性

```
{ :book1 dc:title|rdfs:label ?displayString }
```

可以这样写:

```
{ :book1 <http://purl.org/dc/elements/1.1/title> | <http://www.w3.org/2000/01/rdf-schema#label> ?displayString }
```

顺序:找到爱丽丝认识的所有人的名字。

```
 {
    ?x foaf:mbox <mailto:alice@example> .
    ?x foaf:knows/foaf:name ?name .
  }
```

序列:找到“`foaf:know`”链接的2个人（找到一个人朋友的朋友）。

```
 { 
    ?x foaf:mbox <mailto:alice@example> .
    ?x foaf:knows/foaf:knows/foaf:name ?name .
  }
```

这与SPARQL查询相同:

```
SELECT ?x ?name 
  {
     ?x  foaf:mbox <mailto:alice@example> .
     ?x  foaf:knows [ foaf:knows [ foaf:name ?name ]]. 
  }
```

或者，使用显式变量:

```
SELECT ?x ?name
  {
    ?x  foaf:mbox <mailto:alice@example> .
    ?x  foaf:knows ?a1 .
    ?a1 foaf:knows ?a2 .
    ?a2 foaf:name ?name .
  }
```

过滤重复:因为Alice认识的人很可能认识Alice，所以上面的例子可能包括Alice本人。这可以通过以下方式避免:

```
{ ?x foaf:mbox <mailto:alice@example> .
    ?x foaf:knows/foaf:knows ?y .
    FILTER ( ?x != ?y )
    ?y foaf:name ?name 
  }
```

反向属性路径:这两个是相同的查询:第二个只是反转属性方向，交换主语和宾语的角色。

```
{ ?x foaf:mbox <mailto:alice@example> }
```
```
{ <mailto:alice@example> ^foaf:mbox ?x }
```

逆路径序列:找到所有?y认识某个人，同时?x也认识这个人的人：

```
 {
    ?x foaf:knows/^foaf:knows ?y .  
    FILTER(?x != ?y)
  }
```

它等于(?gen1是一个系统生成变量):

```
{
    ?x foaf:knows ?gen1 .
    ?y foaf:knows ?gen1 .  
    FILTER(?x != ?y)
  }
```

任意长度匹配:通过`foaf:know`Alice可以联系到的所有人的名字:

```
 {
    ?x foaf:mbox <mailto:alice@example> .
    ?x foaf:knows+/foaf:name ?name .
  }
```

任意长度路径的备选方案:

```
 { ?ancestor (ex:motherOf|ex:fatherOf)+ <#me> }
```

任意长度路径匹配:一些形式的有限推理也是可能的。例如，对于RDFS，一个资源的所有类型和超类型:

```
 { <http://example/thing> rdf:type/rdfs:subClassOf* ?type }
```

所有资源及其推断类型:

```
 { ?x rdf:type/rdfs:subClassOf* ?type }
```

子属性:

```
 { 
   ?x ?p ?v . 
   ?p rdfs:subPropertyOf* :property 
 }
```

求反的属性路径：查找已连接但不是通过rdf:type（任其一）：

```
  { ?x !(rdf:type|^rdf:type) ?y }
```

RDF集合中的元素:

```
{ :list rdf:rest*/rdf:first ?element }
```

注意:这个路径表达式不能保证结果的顺序。

### 9.3 属性路径和等效模式

SPARQL属性路径将RDF三元组视为带有命名边的有向(可能是循环的)图。有些属性路径相当于转换成三元组模式和SPARQL联合图模式。对属性路径表达式求值可能会导致重复，因为在等效模式中引入的任何变量都不是结果的一部分，也没有在其他地方使用过。通过将结果隐式地投影到查询中给定的变量，它们被隐藏了。

例如，关于数据:

```
@prefix :       <http://example/> .

:order  :item :z1 .
:order  :item :z2 .

:z1 :name "Small" .
:z1 :price 5 .

:z2 :name "Large" .
:z2 :price 5 .
```

查询语句：

```
PREFIX :   <http://example/>
SELECT * 
{  ?s :item/:price ?x . }
```

查询结果：

|           s            |  x   |
| :--------------------: | :--: |
| <http://example/order> |  5   |
| <http://example/order> |  5   |

然而，如果查询被写出来包含中间变量(?_a)，结果中没有重复的行:

```
PREFIX :   <http://example/>
SELECT * 
{  ?s :item ?_a .
   ?_a :price ?x . }
```

查询结果：

|           s            |         _a          |  x   |
| :--------------------: | :-----------------: | :--: |
| <http://example/order> | <http://example/z1> |  5   |
| <http://example/order> | <http://example/z2> |  5   |

当查询还涉及聚合操作时，与图形模式的等效性尤其重要。订单的总成本可以在下面找到

```
PREFIX :   <http://example/>
  SELECT (sum(?x) AS ?total)
  { 
    :order :item/:price ?x
  }
```

| total |
| :---: |
|  10   |

### 9.4 任意长度路径匹配

可以使用“零或多个”属性路径操作符`*`和“一个或多个”属性路径操作符`+`来找到主题和对象之间任意长度的属性路径连接。还有一个“0或1”连接性属性路径操作符?。

这些操作符都使用属性路径表达式来尝试查找subject和object之间的连接，由于操作符的限制，多次使用路径步骤。

例如，找到一个资源的所有可能类型，包括资源的超类型，可以用:

```
PREFIX  rdfs:   <http://www.w3.org/2000/01/rdf-schema#> . 
PREFIX  rdf:    <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT ?x ?type
{ 
  ?x rdf:type/rdfs:subClassOf* ?type
}
```

类似地，找到:x所有通过foaf:knows关系连接到的人，

```
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX :     <http://example/>
SELECT ?person
{ 
  :x foaf:knows+ ?person
}
```

即使重复路径本身会导致重复，这种连接匹配也不会引入重复(它不会包含任何可以建立连接的方式的计数)。

匹配的图可能包括循环。定义连接性匹配是为了使匹配周期不会导致未定义或无限的结果。

## 10. 任务

表达式的值可以通过将一个新变量绑定到表达式(一个RDF术语)的值来添加到解决方案映射中。该变量可以在查询中使用，也可以在结果中返回。

有三种语法形式允许这样做:BIND关键字、SELECT子句中的表达式和GROUP BY子句中的表达式。赋值形式为(表达式为?var)。

如果表达式的求值产生错误，则该变量对于该解决方案仍未绑定，但查询求值将继续。

还可以使用内联数据的值直接将数据包含在查询中。

### 10.1 BIND：分配变量

BIND形式允许将值分配给基本图模式或属性路径表达式中的变量。使用BIND结束了前面的基本图形模式。在BIND中使用之前，由BIND子句引入的变量必须没有在组图形模式中使用。

Example:

Data:

```
@prefix dc:   <http://purl.org/dc/elements/1.1/> .
@prefix :     <http://example.org/book/> .
@prefix ns:   <http://example.org/ns#> .

:book1  dc:title     "SPARQL Tutorial" .
:book1  ns:price     42 .
:book1  ns:discount  0.2 .

:book2  dc:title     "The Semantic Web" .
:book2  ns:price     23 .
:book2  ns:discount  0.25 .
```

Query:

```
PREFIX  dc:  <http://purl.org/dc/elements/1.1/>
PREFIX  ns:  <http://example.org/ns#>

SELECT  ?title ?price
{  ?x ns:price ?p .
   ?x ns:discount ?discount
   BIND (?p*(1-?discount) AS ?price)
   FILTER(?price < 20)
   ?x dc:title ?title . 
}
```

等效查询(BIND结束基本图形模式;FILTER应用于整个组图模式):

```
PREFIX  dc:  <http://purl.org/dc/elements/1.1/>
PREFIX  ns:  <http://example.org/ns#>

SELECT  ?title ?price
{  { ?x ns:price ?p .
     ?x ns:discount ?discount
     BIND (?p*(1-?discount) AS ?price)
   }
   {?x dc:title ?title . }
   FILTER(?price < 20)
}
```

Results:

|       title        | price |
| :----------------: | :---: |
| "The Semantic Web" | 17.25 |

